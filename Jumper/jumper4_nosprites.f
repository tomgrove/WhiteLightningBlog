0 VARIABLE LASTHERE 
0 VARIABLE FIRSTHERE 

HERE DUP LASTHERE ! FIRSTHERE !

	
." sprite creation "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

HEX

: UNROLL-RSHIFTS 0 DO  CB C, 2C C, 
					  CB C, 1D C,  LOOP ;
					  
: UNROLL-LSHIFTS 0 DO  CB C, 15 C, 
					   CB C, 14 C,  LOOP ;

: 8MODX CREATE ;CODE E1 C, 
					26 C, 0 C,
					3E C, 7 C,
					A5 C,
					6F C,
					E5 C,
					C3 C, 45 C, 61 C, 
				SMUDGE
							
					
: /2X  CREATE ;CODE E1 C, 
					AF C,
					1 UNROLL-RSHIFTS
					E5 C,
					C3 C, 45 C, 61 C, 
			  SMUDGE
			  
: *2X  CREATE ;CODE E1 C, 
					AF C,
					1 UNROLL-LSHIFTS
					E5 C,
					C3 C, 45 C, 61 C, 
			  SMUDGE
			  
: *64X  CREATE ;CODE E1 C, 
					AF C,
					6 UNROLL-LSHIFTS
					E5 C,
					C3 C, 45 C, 61 C, 
			  SMUDGE
			  
			  
: /8X  CREATE ;CODE E1 C, 
					AF C,
					3 UNROLL-RSHIFTS
					E5 C,
					C3 C, 45 C, 61 C, 
			  SMUDGE
			  
: /8-1X  CREATE ;CODE E1 C, 
					AF C,
					3 UNROLL-RSHIFTS
					2B C,
					E5 C,
					C3 C, 45 C, 61 C, 
			  SMUDGE
			  
: X0! CREATE ;CODE 	E1 C, 
					36 C, 0 C,
					23 C,
					36 C, 0 C,
					C3 C, 45 C, 61 C, 
				SMUDGE
					
					
			  
/2X  /2 SMUDGE

/8X  /8 SMUDGE 
 
*2X *2 SMUDGE 

*64X *64 SMUDGE 
 
/8-1X /8-1 SMUDGE

X0! 0!  SMUDGE 

8MODX 8MOD SMUDGE

DECIMAL 

." custom assembly "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

: <STRUCT 
	<BUILDS 0 , HERE 2 -
DOES>
	@ ;
	
: | SWAP DUP @ <BUILDS ,  DUP ROT SWAP +! DOES>  @ + ;

: W| 2 | ;
: C| 1 | ;

: STRUCT> DROP ;

: [] SWAP <BUILDS DUP , * 1 + ALLOT  DOES> DUP @ ROT * + 2 +  ; ( SMUDGE )
		
( : $ <BUILDS 34 WORD HERE C@ 1 + ALLOT DOES> COUNT ; )


1 CONSTANT ?SPRITE-ISALIVE

<STRUCT OBJ-STRUCT
	W| OBJ-SPN
	W| OBJ-X
	W| OBJ-Y
	W| OBJ-OLDX
	W| OBJ-DX
	W| OBJ-DY
	W| OBJ-CXMIN
	W| OBJ-CYMIN
	W| OBJ-CXMAX
	W| OBJ-CYMAX
	W| OBJ-TICK
	C| OBJ-FLAGS
STRUCT>

0 CONSTANT E-PATROLTYPE
1 CONSTANT E-KEYTYPE 

<STRUCT DESC-STRUCT
	C| DESC-SPN
	C| DESC-X
	C| DESC-Y
	C| DESC-DX
	C| DESC-DY
	C| DESC-C1
	C| DESC-C2
	C| DESC-TYPE
STRUCT>

." type extensions "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

0 VARIABLE CURRENT-DESC

0 CONSTANT E-WALKING
1 CONSTANT E-JUMPING
2 CONSTANT E-FALLING
0 VARIABLE JUMP
0 VARIABLE JUMP-INDEX
0 VARIABLE START-JUMP
0 VARIABLE PLAYER-HITX
0 VARIABLE PLAYER-HITY
0 VARIABLE KEY-COUNT
0 VARIABLE DEAD
0 VARIABLE LEVEL
0 VARIABLE LIVES 
0 VARIABLE TILEMAP-PTR

." variables "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

( 1 512 [] TILES  )

." tile array "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

7 CONSTANT MAX-SPRITES
OBJ-STRUCT MAX-SPRITES [] GAME-SPRITES

0 VARIABLE LAST-SPRITE
MAX-SPRITES GAME-SPRITES LAST-SPRITE !

." sprite array "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

0 VARIABLE DECODE-PTR 

: DECODE-PTR, 
	DECODE-PTR @ C!
	2 DECODE-PTR +!
;

( usage -- src srclen dst )

: RL-DECODE 	DECODE-PTR ! OVER + 1 + SWAP ( 2DUP  U. U. )
				DO I  C@ 128  AND 0= IF 
									I C@  DECODE-PTR, R> 1 + >R  
							  ELSE 		
									I C@  127 AND  R> 1 + >R  0 DO   
																	J C@ DECODE-PTR, 
																  LOOP  R> 1 + >R  
							 ENDIF 
			0 +LOOP ;
			
: GET-NUM-LEVELS 		13 SPN ! TEST DROP DPTR @  C@ ;
: GET-LEVEL  			2 * 1 + 13 SPN ! TEST DROP DPTR @  +  @ DPTR @  + ;
: GET-NAME-FIELD  		DUP @ + 2 + ;
: GET-NAME  	  		GET-NAME-FIELD DUP 1 + SWAP C@ ;
: GET-KEYS-FIELD  		GET-NAME-FIELD DUP C@ + 1 + ;
: GET-NUMSPRITE-DESCS 	GET-KEYS-FIELD 1 + C@ ;
: GET-SPRITE-DESC 		GET-KEYS-FIELD 2 + SWAP  8 * + ;

: DECODE LEVEL @ GET-LEVEL DUP @ SWAP 2 + SWAP TILEMAP-PTR @  RL-DECODE ;

: SET-TILEMAP-PTR 251 SPN ! TEST DROP DPTR @ TILEMAP-PTR ! ;
: GET-CELL *64 SWAP *2 + TILEMAP-PTR @ + C@ ; 
: SET-CELL *64 SWAP *2 + TILEMAP-PTR @ + C! ; 
: GET-FLAG *64 SWAP *2 1 + + TILEMAP-PTR @ + C@ ; 
: SET-FLAG *64 SWAP *2 1 + + TILEMAP-PTR @ + C! ; 
: CLR-CELL *64 SWAP *2 + TILEMAP-PTR @ + 0 SWAP ! ;

." level decomp "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

0 VARIABLE Y
0 VARIABLE DY

2 24 [] JUMP-CURVE SMUDGE

: PLOT-CURVE 0 Y ! 3 16 * DY ! 12 0 DO  DY @ Y  +! Y @ 16 / DUP  I JUMP-CURVE ! 23 I - JUMP-CURVE !  ( I . ) ( I ) ( JUMP-CURVE @ .)  -4 ( CR ) DY +! LOOP ; 
 PLOT-CURVE  

." jump curve "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

( DRAW-CURVE 24 0 DO I JUMP-CURVE @ I . . CR LOOP ; )

: PLAYER-SELECT-SPRITE >R  R OBJ-X @ 8MOD /2  
						   R OBJ-SPN @ JUMP @ 0= IF 
														+ 
													ELSE  
														R OBJ-Y  @ 8MOD /2 *2 *2  + +  
												    ENDIF 									  
 R> DROP ;
	
: GET-TILE  SROW ! LEVEL @ 3 MOD  SCOL ! 1 HGT ! 1 LEN ! 252 SP1 ! 6 SP2 ! PWATTM PWBLM ;

: *24 *2 *2 *2 DUP *2 + ;

 ( : GET-TILE-ADDR *24 Y @ + ; )
 : GET-CELL-ADDR *64 *2 *2  + 254 SPN ! TEST DROP DPTR @ + ; 
 
 : CP-TL DUP 256 + SWAP DO DECODE-PTR @ C@ I C! 3 DECODE-PTR +! 32 +LOOP ;
 : CP-TL2 ( GET-TILE-ADDR ) *24 Y @ + DECODE-PTR ! ( GET-CELL-ADDR ) CP-TL ;


( SET-TILE assumes that hgt and len are 1 )

: SET-TILE  SROW !  SCOL !  254 SP2 ! 252 SP1 ! GWATTM GWBLM  ;

: PLAYER-DRAW
>R 
	 R OBJ-Y @  /8-1 SROW !  
	 R OBJ-X @  /8-1 SCOL ! 
	6 LEN ! 5 HGT ! 253 SP1 ! 254 SP2 !  
	PWBLM PWATTM  
	
	1 SCOL ! 1 SROW ! 
	R PLAYER-SELECT-SPRITE  SP1 !
	253 SP2 ! 
	GWORM  
	R OBJ-Y @ /8-1  ROW ! 
	R OBJ-X @ /8-1  COL !  
	253 SPN ! PUTBLS  
R> DROP ;
	
." player draw "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 
	
: PLAYER-DO-JUMP
>R	
	E-JUMPING JUMP ! 
	JUMP-INDEX 0!
	R OBJ-Y @ START-JUMP !
	
	6 2 KB IF 
		176 R OBJ-SPN !
		-2 R OBJ-DX !  
	ELSE 
	6 1 KB IF
		 144 R OBJ-SPN !
		2 R OBJ-DX ! 
	ELSE
		R OBJ-SPN @ 16 OR  R OBJ-SPN !
		R OBJ-DX 0!
	ENDIF 
	ENDIF 
	
R> DROP ;
	
: PLAYER-DO-WALK
	>R
	6 2 KB IF 
		160 R OBJ-SPN !
		-2 R OBJ-X +!  
	ELSE 
	6 1 KB IF 
		128 R OBJ-SPN !
		2 R OBJ-X +! 
	ELSE
		R OBJ-SPN @ 239 AND  R OBJ-SPN !
	ENDIF
	ENDIF 
	R> DROP
;

." player input "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

: INC-CELL 2DUP GET-CELL 1 + DUP 14 = IF 
										DROP CLR-CELL 
									  ELSE 
										ROT ROT SET-CELL 
									  ENDIF ;

: TRY-CRUMBLE 
	2DUP 
 
	GET-CELL 5 > IF
		2DUP 
		2DUP 
		INC-CELL
		GET-CELL 
		GET-TILE 
		SET-TILE 
	ELSE
		DROP DROP
	ENDIF 
;

0 VARIABLE GROUND-FLAG 
0 VARIABLE CEILING-FLAG 
0 VARIABLE WALL-FLAG 

	
: OR! DUP @ ROT OR SWAP ! ;

 ( : DBG-DRAWCELL ROW ! COL ! 1 LEN ! 1 HGT ! INVV ; )

: PLAYER-CHECK-GROUND >R  R OBJ-Y @ 16  + /8  DUP DUP
						  R OBJ-X @ 4   + /8  SWAP     GET-FLAG  GROUND-FLAG OR!
						  R OBJ-X @ 12  + /8  SWAP     GET-FLAG  GROUND-FLAG OR!
						  R OBJ-X @ 20  + /8  SWAP     GET-FLAG  GROUND-FLAG OR!				 
R> DROP ;
						 
: PLAYER-CRUMBLE-GROUND >R  R OBJ-Y @ 16 + /8  DUP DUP 
						    R OBJ-X @ 4  + /8  SWAP  TRY-CRUMBLE
						    R OBJ-X @ 12 + /8  SWAP  TRY-CRUMBLE 
						    R OBJ-X @ 20 + /8  SWAP  TRY-CRUMBLE 
R> DROP ; 	 
						 
: PLAYER-CHECK-CEILING >R  R OBJ-Y @ 1 -  /8 DUP DUP  
						   R OBJ-X @ 4 +  /8   SWAP    GET-FLAG  CEILING-FLAG OR!
						   R OBJ-X @ 12 + /8   SWAP    GET-FLAG  CEILING-FLAG OR!
					       R OBJ-X @ 20 + /8   SWAP    GET-FLAG  CEILING-FLAG OR! 
R> DROP ;

: PLAYER-CHECK-WALL >R 
						R OBJ-SPN @ 32 AND  IF 
												R OBJ-X @ ( 0 + ) /8  
											ELSE 
												R OBJ-X @ 24 + /8 
											ENDIF
					 DUP
					 R OBJ-Y @ ( 0 + )  /8 GET-FLAG  WALL-FLAG OR!
					 R OBJ-Y @ 8 +  /8     GET-FLAG  WALL-FLAG OR!
 R> DROP ; 
		
." player checks  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 		

: PLAYER-JUMPING-HITS 
>R 
	JUMP-INDEX @ 12 > IF
		R PLAYER-CHECK-GROUND  
		GROUND-FLAG @ 8 AND IF
				1 DEAD !
			ELSE
			GROUND-FLAG @ IF 
				R OBJ-SPN  @ 239 AND R OBJ-SPN !
				E-WALKING JUMP !
			ENDIF 
			ENDIF 
	ELSE
		  R PLAYER-CHECK-CEILING 
		 CEILING-FLAG @ 8 AND IF
			1 DEAD !
		 ELSE 
		 CEILING-FLAG @ 1 AND IF 
			 R OBJ-DX 0! 
			 12 JUMP-INDEX @ - 11  + JUMP-INDEX ! 
			 ENDIF
		 ENDIF 
	ENDIF 
		
	R PLAYER-CHECK-WALL  
	WALL-FLAG @ 1 AND IF 
		 R OBJ-OLDX @ R OBJ-X !
	ELSE  WALL-FLAG @ 8 AND IF
		1 DEAD !
		ENDIF
	ENDIF
R> DROP ;

: PLAYER-FALLING-HITS 
>R
	R PLAYER-CHECK-GROUND  
	GROUND-FLAG @ IF
		R OBJ-SPN @ 239 AND R OBJ-SPN !
		E-WALKING JUMP !
	ENDIF 
R> DROP ;

: PLAYER-WALKING-HITS 
>R
	R PLAYER-CHECK-GROUND 

	GROUND-FLAG @ 0=  IF
		R OBJ-SPN  @ 16 OR R OBJ-SPN !
		E-FALLING JUMP !
		ELSE GROUND-FLAG @ 8 AND IF
		 1 DEAD ! 
	ELSE GROUND-FLAG @ 4 AND IF 
		-1 R OBJ-X +!
	ELSE    
			GROUND-FLAG @ 3 AND IF
				NOOP
			ELSE
				GROUND-FLAG @ 16 AND IF
					R PLAYER-CRUMBLE-GROUND
				ENDIF
			ENDIF 
	ENDIF 
	ENDIF 
	ENDIF 
		
	R PLAYER-CHECK-WALL  
	WALL-FLAG @ 1 AND IF 
		R OBJ-OLDX @ R OBJ-X !
	 ELSE  WALL-FLAG @ 8 AND IF
		1 DEAD !
		ENDIF
	ENDIF	
	
R> DROP ;
	
: PLAYER-HITS 
	 GROUND-FLAG 0!
	 CEILING-FLAG 0! 
	 WALL-FLAG 0!
	JUMP @ CASE 
		E-JUMPING OF PLAYER-JUMPING-HITS ENDOF
		E-WALKING OF PLAYER-WALKING-HITS ENDOF 
		E-FALLING OF PLAYER-FALLING-HITS ENDOF
	ENDCASE ;
		
." player hits  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 

: PLAYER-JUMPING
>R
	START-JUMP @ JUMP-INDEX @ JUMP-CURVE @ - R OBJ-Y !
	1 JUMP-INDEX +!
	R OBJ-DX @ R OBJ-X +!
	JUMP-INDEX @ 24 = IF
		E-WALKING JUMP !
		START-JUMP @  R OBJ-Y !
	ENDIF 
R> DROP ;
	
: PLAYER-FALLING  3 SWAP OBJ-Y +!  ;

		  
: PLAYER-TICK
	>R
	R OBJ-X @ R OBJ-OLDX !
	JUMP @ CASE 
	E-WALKING OF
			3 1  KB IF
				R PLAYER-DO-JUMP
			ELSE
				R PLAYER-DO-WALK
			ENDIF 
		ENDOF
	E-JUMPING OF  R PLAYER-JUMPING ENDOF 
	E-FALLING OF  R PLAYER-FALLING ENDOF 
	ENDCASE 

	R PLAYER-HITS  
	
	R OBJ-X @ 12 + PLAYER-HITX !
	R OBJ-Y @ 8 + PLAYER-HITY !
	
	R PLAYER-DRAW
	
	R> DROP ;

." player move "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - .  HERE U. 

: TICK-SPRITE DUP  OBJ-TICK @ CFA EXECUTE ;

: INIT-GAME-SPRITES  
	MAX-SPRITES 0 DO 0 
					I GAME-SPRITES OBJ-FLAGS ! 
				LOOP  ;
				
: FIND-FREE 
	MAX-SPRITES 0 DO 
		I GAME-SPRITES 
			DUP OBJ-FLAGS @ ?SPRITE-ISALIVE AND 0= IF 
														LEAVE 
													  ELSE 
														DROP 
													  ENDIF 
	LOOP ; 

: TICK-SPRITES  
	0 GAME-SPRITES >R 
	BEGIN 
		R OBJ-FLAGS @ ?SPRITE-ISALIVE AND IF 
												R TICK-SPRITE 
											 ENDIF 
		R> OBJ-STRUCT + >R
	R LAST-SPRITE @ = UNTIL 
	R> DROP  ; 	

: T2FLG
	CASE 
	0 OF 0 ENDOF 
	1 OF 1 ENDOF
	2 OF 2 ENDOF 
	3 OF 4 ENDOF 
	4 OF 8 ENDOF
	5 OF 8 ENDOF
	6 OF 16 ENDOF 
	ENDCASE ;

0 VARIABLE TILE-PTR

: SET-TILES 
	6 SPN ! TEST DROP DPTR @ LEVEL @ + Y !
	16 0 DO  
		0 I  GET-CELL-ADDR TILE-PTR !
		32 0 DO  I J 
			  GET-CELL DUP GET-TILE   ( TILE-PTR @ SWAP CP-TL2 )   I J SET-TILE  T2FLG  I J SET-FLAG 	
			  1 TILE-PTR +!
		LOOP 
		 (  ATTON 254 SPN ! 0 COL ! 0 ROW ! PUTBLS   )
	LOOP ; 

." sprite tick "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
	
: ENEMY-DRAW   >R  R OBJ-Y @ /8  ROW ! R OBJ-X @ DUP /8  COL !  8MOD /2 R OBJ-SPN @ + SPN ! ATTOFF PUTBLS   R>   DROP ;

." sprite draw "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
		  
: CHECK  >R
			R OBJ-X @ R OBJ-CXMIN @ + PLAYER-HITX @ > IF 0 
			ELSE
			R OBJ-X @ R OBJ-CXMAX @ + PLAYER-HITX @ < IF 0 
			ELSE
			R OBJ-Y @ R OBJ-CYMIN @ + PLAYER-HITY @ > IF 0 
			ELSE
			R OBJ-Y @ R OBJ-CYMAX @ + PLAYER-HITY @ > 
			ENDIF 		
			ENDIF
			ENDIF 
			( R DBG-DRAW-HITS  )			
R> DROP ;
	
." sprite check "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: NULL-TICK DROP ;
	
: ENEMY-TICK  >R  
			  R OBJ-DX @ R OBJ-X +!  
			  -1 R OBJ-DY +!
				R OBJ-DY @ 0= IF 
					 R OBJ-OLDX @ R OBJ-DY ! 
					 R OBJ-DX @ MINUS R OBJ-DX ! 
				ENDIF
			
			 R CHECK  IF
				 1 DEAD ! 
			 ENDIF 
			
			R ENEMY-DRAW 
			( R DBG-DRAW-HITS )
				
 R> DROP ;
	
." enemy tick "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
	
: DOOR-TICK >R
		 R CHECK IF
				 2 DEAD ! 
		 ENDIF 
		 
		 ( R DBG-DRAW-HITS )
R> DROP ;

." door tick "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
	
: MAKE-DOOR
FIND-FREE >R
	12  R OBJ-SPN !
	R OBJ-Y !
	R OBJ-X !
	8 8 - R OBJ-CXMIN  ! 
	8 8 - R OBJ-CYMIN  ! 
	8 8 + R OBJ-CXMAX  ! 
	8 8 + R OBJ-CYMAX  ! 
	' DOOR-TICK R OBJ-TICK !
	1 R OBJ-FLAGS C! 
	R OBJ-X @ /8 SCOL !  R OBJ-Y @ /8 SROW !  254 SP2 ! R OBJ-SPN @  SP1 ! GWATTM GWBLM  	
	R OBJ-X @ /8 COL  ! R OBJ-Y  @ /8 ROW  !  R OBJ-SPN @ SPN ! PUTBLS
R> DROP ;


." door make "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: KEY-TICK  >R   
	R OBJ-X @ /8 COL ! R OBJ-Y @ /8 ROW ! 1 HGT ! 1 LEN ! R OBJ-OLDX @ 7 AND INK SETAV
	1 R OBJ-OLDX +!
	R CHECK  IF 
			0 10 DO 10 I 100 *  BLEEP LOOP 
			0 R OBJ-FLAGS C! 
			-1 KEY-COUNT +!
			0 GET-TILE R OBJ-X @ /8  R OBJ-Y @ /8 SET-TILE
			R OBJ-X @ /8 COL ! R OBJ-Y @ /8 ROW !  7 INK SETAV
			KEY-COUNT @ 0= IF 
				R OBJ-DX @ R OBJ-DY @ MAKE-DOOR 
			ENDIF 
	ENDIF
	 ( R DBG-DRAW-HITS )
R> DROP ;

." key tick "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: SET-COMMON >R 
	CURRENT-DESC @ DESC-SPN C@ R OBJ-SPN !
	CURRENT-DESC @ DESC-Y 	C@ R OBJ-Y !
	CURRENT-DESC @ DESC-X 	C@ R OBJ-X !
	1 R OBJ-FLAGS C!
R> DROP ;

." common  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: MAKE-KEY 
	FIND-FREE >R
	R SET-COMMON
	CURRENT-DESC @ DESC-C1 	C@ R OBJ-DX !
	CURRENT-DESC @ DESC-C2 	C@ R OBJ-DY !
	0 R OBJ-OLDX !
	4 8 - R OBJ-CXMIN ! 
	4 8 - R OBJ-CYMIN ! 
	4 8 + R OBJ-CXMAX ! 
	4 8 + R OBJ-CYMAX !
	' KEY-TICK R OBJ-TICK !
	R OBJ-X @ /8  SCOL !  R OBJ-Y @ /8 SROW !  254 SP2 ! R OBJ-SPN @  SP1 ! GWATTM GWBLM  	
	SCOL @ COL ! SROW @ ROW ! SP1  @ SPN ! PUTBLS 
	R> DROP 
	;
	
." make key  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
	
: MAKE-PLAYER 
	FIND-FREE >R
	128 R OBJ-SPN !
	10 	R OBJ-X !
	106 R OBJ-Y !
	' PLAYER-TICK R OBJ-TICK !
	1 R OBJ-FLAGS C! 
	R> DROP ;
	
." make player  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 
	
: MAKE-ENEMY 
	FIND-FREE >R
	R SET-COMMON
	CURRENT-DESC @ DESC-DX 	C@ R OBJ-DX !
	CURRENT-DESC @ DESC-DY 	C@ DUP R OBJ-DY ! R OBJ-OLDX !
	8 8 - R OBJ-CXMIN  !
	4 8 - R OBJ-CYMIN  !
	8 8 + R OBJ-CXMAX !
	4 8 + R OBJ-CYMAX  !
	' ENEMY-TICK R OBJ-TICK !
	R> DROP ;

." make enemy  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: MAKE-SPRITE 
	DUP CURRENT-DESC !
	DESC-TYPE C@  CASE 
	E-KEYTYPE OF    	MAKE-KEY ENDOF 
	E-PATROLTYPE OF     MAKE-ENEMY ENDOF
	ENDCASE
;


	
: MAKE-SPRITES LEVEL @ GET-LEVEL GET-NUMSPRITE-DESCS 0  DO 
	I LEVEL @ GET-LEVEL GET-SPRITE-DESC MAKE-SPRITE 
	LOOP 
	;

." make sprites  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: DRAW-BACKGROUND ATTON 254 SPN ! 0 COL ! 0 ROW ! PUTBLS ;

( 0 VARIABLE FRAMES  )
( 0 VARIABLE LAST-FRAMES  )

( : COUNTER FRAMES 1 FRAMES +! ; )

( : SHOW-COUNTER FRAMES @ LAST-FRAMES @ - 17 0 AT . FRAMES @ LAST-FRAMES ! ; )

( : START-COUNTER  ' COUNTER INT-ON ; )

( : INNER-LOOP TICK-SPRITES   ; )

: PRINT-NAME   LEVEL @ GET-LEVEL GET-NAME 17 0 AT TYPE  ;

: PRINT-CRAWL 7 INK 17 0 AT LEVEL @ GET-LEVEL GET-NAME DROP + 32 TYPE ;

: SHOW-LIVES   
	19 ROW ! 129 SPN ! 
	LIVES @ DUP 1 > IF 
						1 - 0 DO 
							I 5 * COL ! PUTBLS 
						LOOP 
					ELSE 
						DROP 
					ENDIF  ;

: SETUP-LEVEL 
	0 BORDER 7 INK 0 PAPER CLS 
	INIT-GAME-SPRITES 
	DECODE 
	SET-TILES 
	LEVEL @ GET-LEVEL GET-KEYS-FIELD C@  KEY-COUNT ! 
	DRAW-BACKGROUND  
	MAKE-SPRITES ;

: NEW-LIFE ( 0 FRAMES ! ) 
	E-WALKING JUMP ! 
	SETUP-LEVEL  
	PRINT-NAME  
	SHOW-LIVES  
	MAKE-PLAYER  
	0 DEAD !  ;

: FADE  0 7 DO I INK 32 LEN ! 17 HGT ! 0 COL ! 0 ROW ! SETAV 100 10 I * BLEEP -1 +LOOP   ;
: FLASHV  10 0  DO  32 LEN ! 17 HGT ! 0 COL ! 0 ROW ! INVV 10 10 I * BLEEP LOOP  ;

: OUTER-LOOP NEW-LIFE DI ( START-COUNTER ) BEGIN  TICK-SPRITES   ( SHOW-COUNTER ) DEAD @ UNTIL ;

." loop code  "  HERE LASTHERE @  - U. CR HERE LASTHERE ! HERE FIRSTHERE @ - . 

: SCROLL-CRAWL 32 LEN ! 1 HGT ! 0 COL ! 17 ROW ! SCL1V ;

: TITLE  10 LEVEL ! 
				SETUP-LEVEL  
				96 0 DO 
					I PRINT-CRAWL 
					8 0 DO 
						TICK-SPRITES 
						SCROLL-CRAWL 
						3 1  KB IF LEAVE ENDIF 
						LOOP 
					3 1  KB IF LEAVE  ENDIF 
				LOOP ;

: ATTRACT  10 0 DO 
					TITLE 
					3 1 KB IF LEAVE ENDIF 
					I LEVEL ! 
					SETUP-LEVEL 
					PRINT-NAME 
					1000 0  DO 
							TICK-SPRITES  
							3 1 KB IF LEAVE ENDIF 
						LOOP  FLASHV  
					3 1  KB IF LEAVE ENDIF 
				LOOP   ;

: MAIN  ( SETUP-SPRITES  )
		SET-TILEMAP-PTR
		ATTRACT 
		0 LEVEL !
		3 LIVES !
	BEGIN
		OUTER-LOOP 
		DEAD @ CASE 
			1 OF FADE  -1 LIVES +! ENDOF 
			2 OF FLASHV 1 LEVEL +!  LEVEL @ 10 MOD LEVEL !  ENDOF 
		ENDCASE 
		LIVES @ 0= IF 
			 32 LEN ! 17 HGT ! 0 COL ! 0 ROW ! 2 PAPER 0  INK CLSV 
			 10 12 AT ." GAME OVER" 
			 EI 100 0 DO HALT LOOP DI 
			0 LEVEL !
			3 LIVES !
			ATTRACT
		ENDIF 
	AGAIN ;


." main "  HERE LASTHERE @  - U. CR HERE LASTHERE ! 
."  "   LASTHERE @ FIRSTHERE @ - U.



0 COL ! 0 ROW ! 



